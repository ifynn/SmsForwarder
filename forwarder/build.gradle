import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

File signingCfg = new File("signing.gradle")

if (signingCfg.exists()) {
    apply from: 'signing.gradle'
}

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.fynn.smsforwarder"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 3
        versionName "1.0.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            zipAlignEnabled true  //是否zip对齐
            minifyEnabled true  //是否混淆代码
            shrinkResources true  //是否删除无用资源文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "短信转移"

            if (signingCfg.exists()) {
                signingConfig signingConfigs.release
            }
        }

        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            zipAlignEnabled false
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            versionNameSuffix "-debug" //版本号后面加-debug标识
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "短信转移-debug"

            if (signingCfg.exists()) {
                signingConfig signingConfigs.debug
            }
        }
    }

    packagingOptions {
        pickFirst 'META-INF/LICENSE.txt' // picks the JavaMail license file
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

def now() {
    def df = new SimpleDateFormat("yyyyMMdd")
    df.setTimeZone(TimeZone.getDefault())
    return df.format(new Date())
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'com.google.android.gms:play-services-plus:11.8.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    // use whatever the current version is...
    compile 'com.sun.mail:android-mail:1.6.0'
    compile 'com.sun.mail:android-activation:1.6.0'
    compile 'org.fynn.appu:appu:1.0.2'
    compile 'com.qmuiteam:qmui:1.0.6'
    compile 'com.fynn.switcher:switch:1.0.2'
}
